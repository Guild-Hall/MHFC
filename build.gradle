buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

apply from: 'build.properties'
apply from: 'release.gradle'

project.version = getVersion()
project.ext.shortVersion = getShortVersion()

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

// We will combine the outputs into one directory, so that it shows up as one entry on the classpath
// This is necessary to load resources with runClient
task combinedSource(type: Sync) {
    from sourceSets.main.output
    into "$buildDir/combined-classpath"
}

sourceSets {
    main {
        runtimeClasspath = files(combinedSource.outputs) + project.configurations.runtimeClasspath
    }
}

minecraft {
    runs {
        client {
            workingDirectory project.file('run')
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            // Debug connection for development
            // jvmArgs += ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006"]

            mods {
                mhfc {
                    source sourceSets.main
                }
            }
        }

        server {
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            // Debug connection for development
            // jvmArgs += ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006"]

            mods {
                mhfc {
                    source sourceSets.main
                }
            }
        }
    }
}

jar {
    manifest {
        attributes([
            "Specification-Title": project.archivesBaseName,
            "Specification-Vendor": "WorldSEnder, Heltrato",
            "Specification-Version": project.shortVersion,
            "Implementation-Title": project.name,
            "Implementation-Version": project.shortVersion,
            "Implementation-Vendor" :"WorldSEnder, Heltrato",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.mcversion

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand project.properties
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task processSource(type: Sync) {
    from(sourceSets.main.java) {
        include 'mhfc/net/common/index/ResourceInterface.java'
        // expand project.properties
    }
    from(sourceSets.main.java) {
        exclude 'mhfc/net/common/index/ResourceInterface.java'
    }
    into "$buildDir/processed-src"
}

compileJava {
    source = processSource.outputs
}

jar.finalizedBy('reobfJar')

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from jar.source
}

build.dependsOn sourceJar

dependencies {
    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourceJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

test {
    testLogging {
        exceptionFormat "full"
    }
    afterTest { desc, result -> 
        println "${desc.className} > ${desc.name} ${result.resultType}"
    }
}

def getShortVersion() {
    def stdout = new ByteArrayOutputStream()
    exec {
        executable "git"
        args "rev-list", "--count", "HEAD"
        standardOutput = stdout
    }
    def revision = stdout.toString().trim()
    return "${major}.${minor}.${patch}.${revision}"
}

def getVersion() {
	return getShortVersion() + getArtifactID() + "+${project.mcversion}"
}

def getArtifactID() {
    def stdout = new ByteArrayOutputStream()
    exec {
        executable "git"
        args "rev-parse", "--abbrev-ref", "HEAD"
        standardOutput = stdout
    }
    def branch = stdout.toString().trim()
    if(branch == "HEAD") {
        stdout.reset()
        exec {
            executable "git"
            args "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        branch = stdout.toString().trim()
    }
    def branchID = branch == 'master' ? '' : '-' + branch.replaceAll('[^0-9A-Za-z-\\.]', '-')
	return [branchID].join('-')
}
