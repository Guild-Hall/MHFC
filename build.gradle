buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

apply plugin: 'maven'
apply plugin: 'net.minecraftforge.gradle.forge'
apply from: 'build.properties'
apply from: 'release.gradle'

allprojects {
    repositories { 
        jcenter()
        mavenCentral()
    }
}

minecraft {
    runDir = "run"
    makeObfSourceJar = true
}

project.version = getVersion()
project.ext.shortVersion = getShortVersion()

minecraft {
    replace "@VERSION@", project.shortVersion
    replace "@MODID@", project.modid
    replace "@MODNAME@", project.longname
    replaceIn "MHFCReference.java"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand project.properties
    }
    
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.install.dependsOn 'build'

// Always run these tasks
tasks.runClient.outputs.upToDateWhen { false }
tasks.runServer.outputs.upToDateWhen { false }
// Debug connection for development
tasks.runClient {
    jvmArgs += ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006"]
}
tasks.runServer {
    jvmArgs += ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5007"]
}

artifacts {
    archives jar
    archives sourceJar
}

test {
    testLogging {
        exceptionFormat "full"
    }
    afterTest { desc, result -> 
        println "${desc.className} > ${desc.name} ${result.resultType}"
    }
}

def getShortVersion() {
    def stdout = new ByteArrayOutputStream()
    exec {
        executable "git"
        args "rev-list", "--count", "HEAD"
        standardOutput = stdout
    }
    def revision = stdout.toString().trim()
    return "${major}.${minor}.${patch}.${revision}"
}

def getVersion() {
	return getShortVersion() + getArtifactID() + "+${minecraft.version}"
}

def getArtifactID() {
    def stdout = new ByteArrayOutputStream()
    exec {
        executable "git"
        args "rev-parse", "--abbrev-ref", "HEAD"
        standardOutput = stdout
    }
    def branch = stdout.toString().trim()
    if(branch == "HEAD") {
        stdout.reset()
        exec {
            executable "git"
            args "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        branch = stdout.toString().trim()
    }
    def branchID = branch == 'master' ? '' : '-' + branch.replaceAll('[^0-9A-Za-z-\\.]', '-')
	return [branchID].join('-')
}
