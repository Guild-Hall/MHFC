import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

apply from: 'build.properties'

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.7.10-10.13.3.1380-1.7.10"
    runDir = "eclipse"

    replace "@VERSION@", project.version
    replace "@MODID@", project.modID
    replace "@MODNAME@", project.longname
    replaceIn "MHFCReference.java"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        filter(ReplaceTokens, tokens: [VERSION: project.version, MCVERSION: project.minecraft.version, MODID: project.modID, MODNAME: project.longname])
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

dependencies {
    compile files('./externlibs/mcanm-0.2.001b_1710-deobf.jar')
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourceJar(type: Jar, dependsOn: 'sourceMainJava') {
    from "build/sources/java"
    classifier = 'sources'
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from 'build/docs/javadoc'
//}

tasks.build.dependsOn('deobfJar', 'sourceJar')

artifacts {
    archives sourceJar
    archives deobfJar
    // archives javadocJar
}
