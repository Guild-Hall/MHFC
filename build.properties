buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

apply plugin: 'de.undercouch.download'
import de.undercouch.gradle.tasks.download.Download

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft.version = "1.12.2-14.23.4.2705"
minecraft.mappings = "stable_39"

archivesBaseName = "mhfc"
group = "com.github.guild-hall.mhfc"

ext {
    major = 1
    minor = 4
    patch = 8
    longname = "Monster Hunter Frontier Craft"
    modid = "mhfc"
}

repositories {
    maven { url "https://jitpack.io" }
    maven {
        name 'sk89q-repo'
        url 'http://maven.sk89q.com/repo/'
    }
}

def worleditFile = new File(buildDir, "worldedit-forge-mc1.12-6.1.8-dist.jar")
task downloadWorleditFromCurse(type: Download) {
    src 'https://minecraft.curseforge.com/projects/worldedit/files/2460570/download'
    dest worleditFile
    onlyIfModified true
}
task verifyWorleditFromCurse(type: Verify, dependsOn: downloadWorleditFromCurse) {
    src worleditFile
    algorithm 'MD5'
    checksum '73ecbb8656a5fd5823852078f0fb7ca7'
}

configurations {
    testCompileOnly.extendsFrom deobfProvided
    testRuntimeOnly.extendsFrom runtime
}

dependencies {
    deobfCompile "com.github.Guild-Hall:MCAnm:v2.7.6"
    // Exclude everything that is found in minecraft
    deobfProvided("com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT") {
        exclude group: "com.google.guava"
        exclude group: "com.google.code.findbugs"
        exclude group: "com.google.code.gson"
    }
    deobfProvided("com.sk89q.worldedit:worldedit-forge-mc1.11:6.1.6") {
        exclude group: "org.spongepowered"
    }
    testCompile "junit:junit:4.8.1"
    // Now we depend on the distribution file from CurseForge
    // "runtime" is deprecated in gradle in favor of "runtimeOnly"
    // but we can't use "runtimeOnly - ForgeGradle won't detect that
    runtime files(worleditFile) {
	    builtBy 'verifyWorleditFromCurse'
    }
}
